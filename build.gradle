buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.31.6"
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'java-library'
apply from: 'gradle/repo.gradle'
apply plugin: "com.jfrog.artifactory"

version = releaseVersion

def dockerUrl = project.hasProperty("docker_url") ? docker_url : System.getenv("docker_url")
def dockerPrivateRegistry = project.hasProperty("docker_private_registry") ? docker_private_registry : System.getenv("docker_private_registry")
def dockerPrivateRegistryUsername = project.hasProperty("docker_private_registry_username") ? docker_private_registry_username : System.getenv("docker_private_registry_username")
def dockerPrivateRegistryPassword = project.hasProperty("docker_private_registry_password") ? docker_private_registry_password : System.getenv("docker_private_registry_password")

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    implementation(group: 'com.google.code.gson', name: 'gson', version: '2.8.9')
    implementation(group: 'org.apache.commons', name: 'commons-compress', version: '1.21')
    implementation(group: 'com.github.zafarkhaja', name: 'java-semver', version: '0.9.0')
    implementation(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.31.5')
}

test {
    systemProperty "docker_url", dockerUrl
    systemProperty "docker_private_registry", dockerPrivateRegistry
    systemProperty "docker_private_registry_username", dockerPrivateRegistryUsername
    systemProperty "docker_private_registry_password", dockerPrivateRegistryPassword

    testLogging {
        events 'started', 'passed', 'skipped', 'failed'
    }
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.register('groovydocJar', Jar) {
    dependsOn groovydoc
    archiveClassifier = 'groovydoc'
    from groovydoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'org.jfrog.util'
            artifactId 'docker-remote-util'
            version version

            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
            artifact groovydocJar {
                classifier "groovydoc"
            }
        }
    }
}

artifacts {
    archives sourcesJar,
            groovydocJar,
            jar

}

artifactory {
    publish {

        defaults {
            publications('mavenJava')
        }
    }
}